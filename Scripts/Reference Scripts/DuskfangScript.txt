scn DuskfangScript

;related to DawnfangScript

short ShouldEquip					;This variable is a flag if the weapon was previously equipped for use when the weapons swap


;This block resets the global variable SESwordDawnfangKills and sets the should equip flag if the previous weapon was equipped

begin OnAdd Player

	Set SESwordDuskfangKills to 0
	if ( SESwordDawnfangEquip == 1 )
		Set ShouldEquip to 1
		Set SESwordDawnfangEquip to 0
	endif

end

;This block sets the equip flag upon equip of this weapon, the second block does the opposite

begin OnEquip Player

	if ( SESwordDuskfangEquip < 1 )
		Set SESwordDuskfangEquip to 1
	endif

end

begin OnUnEquip Player

	if ( SESwordDuskfangEquip > 0 )
		Set SESwordDuskfangEquip to 0
	endif

end

;This block auto-equips the weapon if the should equip flag was set to 1.. it equips the entire list, as we don't know which one the player has


begin gamemode

	if ( ShouldEquip == 1 )
		Player.EquipItem SE03Duskfang01
		Player.EquipItem SE03Duskfang05
		Player.EquipItem SE03Duskfang10
		Player.EquipItem SE03Duskfang15
		Player.EquipItem SE03Duskfang20
		Player.EquipItem SE03Duskfang25
		Player.EquipItem SE03Duskfang30
		Player.EquipItem SE03Duskfang01A
		Player.EquipItem SE03Duskfang05A
		Player.EquipItem SE03Duskfang10A
		Player.EquipItem SE03Duskfang15A
		Player.EquipItem SE03Duskfang20A
		Player.EquipItem SE03Duskfang25A
		Player.EquipItem SE03Duskfang30A
		Set SESwordDuskfangEquip to 1
		Set ShouldEquip to 0
	endif

end